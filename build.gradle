plugins {
    id "org.spongepowered.gradle.vanilla" version "0.2.1-SNAPSHOT"
    id "java"
}

allprojects {
    group = mod_group
    version = mod_version

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    repositories {
        maven { url "https://repo.spongepowered.org/maven" }
        maven { url "https://io.terraforged.com/maven/" }
        mavenCentral()
    }
}

subprojects {
    task commonClasses(type: Copy) {
        from("$rootDir/build/classes/")
        into("$buildDir/classes/")
    }

    task commonResources(type: Copy) {
        from("$rootDir/src/main/resources/")
        into("$buildDir/resources/main/")
    }

    classes.dependsOn(commonClasses)

    processResources.dependsOn(commonResources)
}

configurations {
    library
    implementation.extendsFrom library
}

dependencies {
//    annotationProcessor "org.spongepowered:mixin:0.8.4:processor"
    implementation "org.spongepowered:mixin:0.8.5"
    library "com.terraforged:Engine:0.2.5"
}

task libraries(type: Copy) {
    from { configurations.library.collect { it.isDirectory() ? it : zipTree(it) } }
    into("build/classes/java/main")
    duplicatesStrategy = "EXCLUDE"
}

classes.dependsOn(libraries)

minecraft {
    version(minecraft_version)
}