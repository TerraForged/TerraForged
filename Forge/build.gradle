buildscript {
    repositories {
        maven { url "https://repo.spongepowered.org/maven" }
        maven { url = "https://maven.minecraftforge.net" }
        mavenCentral()
    }
    dependencies {
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT", changing: true
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "eclipse"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    maven { url "https://repo.spongepowered.org/maven" }
    maven { url = "https://maven.fabricmc.net/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.18-${forge_version}"
    implementation rootProject
}

jar {
    archivesBaseName = archives_base_name
    archiveClassifier.set(project.name.toLowerCase())
    finalizedBy("reobfJar")
}

minecraft {
    mappings channel: "official", version: minecraft_version
    runs {
        client {
            workingDirectory project.file("run-client")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            arg "-mixin.config=terraforged.mixins.json"
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run-server")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            arg "-mixin.config=terraforged.mixins.json"
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "mixin.terraforged.refmap.json"
}
